#class {player_heartbeat} {kill}
#class {player_heartbeat} {open}

#var bot_toggle 1;
#var ash_toggle 0;
#var golem_toggle 1;
#var immersion_casts 1;

#alias {_player_heartbeat} {
    #math sp_threshold {$my[sp][max] * .90};
    #math saturation_threshold {$saturation_max * .15};
    #math saturation_danger {$saturation_max * .8};

    #if !$idle_flag {
        #if {($my[sp][current] < $sp_min || $my[gp1][current] > $saturation_max) && !$bot_toggle} {
            #ECHO {Stopping:  SP: $my[sp][current] Saturation: $my[gp1][current]};
            .pause;study spellbook;#var bot_toggle 1};
        #if {$my[sp][current] > $sp_threshold && $my[gp1][current] < $saturation_threshold && $bot_toggle} {
            #ECHO {Resuming:  SP: $my[sp][current] Saturation: $my[gp1][current]};
            ..;#var bot_toggle 0};
    };

    #NOP -- Heartbeat actions that should only be attempted once per round;
    #if {!$idle_flag && $action_round < $mip[round]} {
        #if {$my[gp1][current] > $saturation_danger && $corpses[total] > 0} {corpse_select;#delay 4 {cast corpse blast;cast corpse blast}};
        #if {$my[golem][current] && $golem_toggle && !$my[summon][golem]} {#var golem_toggle 0;create_golem;#delay 20 {#var golem_toggle 1}};
        #if {$my[immersion][count] && $my[golem][reset] > 80} {cast mystic immersion};
        #if {$my[immersion][count] && $my[sp][current] < $sp_min} {cast mystic immersion};
        #if {$my[gp1][current] < 30 && !$ash_toggle} {ash on;ash;#var ash_toggle 1;};
        #if {!$my[super][current] && $my[gp1][current] > $saturation_danger && $ash_toggle} {ash off;#var ash_toggle 0;};
        #if {$my[super][current] && $my[gp1][current] > 60} {cast explosive release};
        #if {$my[g2n] == 100 && $gl_run} {#var gl_run 0;#delay 120 go_glvl};
        #if {$my[gem] < 80 && !$go_fetch} {#var go_fetch 1;ffetch gem};

        #if {$my[immersion][rounds] > 0 && $immersion_casts} {
            cast haste on summons;
            cast haste on oscar;
        }
    };

    #NOP -- If you have low sp, and it's safe to spelltap to renew your defense - do so.
    #if {!$idle_flag && $action_round < $mip[round]} {
		#if {$my[sp][current] > 100 && $my[sp][current] < 400 && $my[gp1][current] < 80} {
			#if {{$defs[MB] && !$my[prots][MB]} || 
			 	{$defs[PA] && !$my[prots][PA]} || 
			 	{$defs[MG] && !$my[prots][MG]} || 
			 	{$defs[mg] && !$my[prots][mg]} || 
			 	{$defs[ms] && !$my[prots][MS]} || 
			 	{$defs[SS] && !$my[prots][SS]} || 
			 	{$defs[rs] && !$my[prots][RS]} || 
			 	{$defs[pe] && !$my[prots][PE]} || 
			 	{$defs[pg] && !$my[prots][PG]} || 
			 	{$defs[shield] && !$my[prots][S]} ||
			 	{$defs[armor] && !$my[prots][A]} || 
			 	{$defs[blink] && !$my[prots][B]}} {cast spell tap}
		}
	};

    #var action_round $mip[round];
};

#class {player_heartbeat} {close}