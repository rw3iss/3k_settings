#class {corpse_manager} {kill}
#class {corpse_manager} {open}

#NOP -- GENERAL CORPSE MANAGEMENT TRACKING FOR HPBAR;

#NOP -- Override the alias for inventory to reset the variables and set alias to pick which corpse to use;
#NOP -- If you have a corpse smuggled, but none elsewhere - smuggle remove it.  Otherwise, use coffin first, then freezer, then inventory;

#alias {i} {#var corpses[inventory] 0;#send i};

#alias corpse_select {
        i;
        #if {$corpses[smuggle] && !$corpses[inventory] && !$corpses[coffin] && !$corpses[freezer]} {smuggle remove corpse;drop corpse;};
        #if $corpses[coffin] {unwrap;} {#if $corpses[freezer] {deslab;} {unkeep corpse;drop corpse;}}
}


#NOP -- Inventory overrides.  These actions update the count when you check inventory;
#act {^ %*Death's Freezer (%1 corpses).$} {
	#if {%1 != $corpses[freezer]} {
		#delay {1} {#echo {<169>Freezer:  Number of corpses thought $corpses[freezer], corrected to %1.};
		#var corpses[freezer] %1};
	} {
		#delay {0.1} {#echo {<169>Freezer:  Correct number of corpses found, $corpses[freezer].}}
	}
};

#act {^ 1 An enchanted coffin (%1 %w)%*.$} {
	#if {%1 != $corpses[coffin]} {
		#delay {1} {#echo {<169>Coffin:  Number of corpses thought $corpses[coffin], corrected to %1.};
		#var corpses[coffin] %1};
	} {
		#delay {0.1} {#echo {<169>Coffin:  Correct number of corpses found, $corpses[coffin].}}
	}
};



#NOP -- Corpse Management COFFIN / FREEZER;
#var corpses[coffin] 0;
#var corpses[freezer] 0;
#act {^You pick up} {#line gag;#echo {<169>Coffin: +1 corpse};#math {corpses[coffin]} {$corpses[coffin] + 1}} {5}
#act {wrap it up into the coffin's protective hold!$} {#line gag}
#act {^The coffin expels} {#line gag;#echo {<169>Coffin: -1 corpse};#math {corpses[coffin]} {$corpses[coffin] -1}}
#act {^You shake the frame and out drops} {#line gag;#echo {<169>Freezer: -1 corpse};#math {corpses[freezer]} {$corpses[freezer] -1}}
#act {^You pick up %* and touch} {#line gag;#echo {<169>Freezer: +1 corpse};#math {corpses[freezer]} {$corpses[freezer] + 1}} {4}
#act {^There are no corpses in the coffin!$} {#var corpses[coffin] 0};


#NOP -- Corpse Management INVENTORY
#var corpses[inventory] 0;
#act {^You drop %* corpse of %*.$} {#math {corpses[inventory]} {$corpses[inventory] - 1}}
#act {^~%* corpse of %*: Taken.} {#math {corpses[inventory]} {$corpses[inventory] + 1}};
#act {^%* corpse of %*: Ok.$} {#math {corpses[inventory]} {$corpses[inventory] + 1}};
#act {^%s%d ~%* corpse of %*.$} {
	#math {corpses[inventory]} {$corpses[inventory] + %2};
	}

#NOP -- Corpse Management SMUGGLE
#var corpses[smuggle] 0;
#act {^Items you are currently smuggling (%*):} {#var corpses[smuggle] 0;};
#act {^> %* corpse of %*} {#math {corpses[smuggle]} {$corpses[smuggle] + 1}};
#act {^You smuggle away: ~%* corpse of %w} {
	#math {corpses[smuggle]} {$corpses[smuggle] + 1};
	#math {corpses[inventory]} {$corpses[inventory] - 1};
	};

#act {^You unsmuggle: %* corpse of %*} {
	#math {corpses[smuggle]} {$corpses[smuggle] - 1};
	#math {corpses[inventory]} {$corpses[inventory] + 1};
	};

#class {corpse_manager} {close}