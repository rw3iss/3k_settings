#class {bot} {open}

#var botting 1;

#var foundmob 0;
#var foundplayer 0;
#var founditems 0;
#var repeat_path 0;

#var prevMobName 0;
#var mobname 0;

#var loop 1;
#var dontAttack 0;
#var pickupAll 0;
#var justItems 0;


/* ignore these players */
#var playerWhitelist {A vicious bear;Nix;Toren;Byron;Martyr;Roor;Void;Killgrave;Zorbaine};
#var mobWhitelist {Almodo;Packy the packmule;Roor;Ivy;Martyr};
#NOP #act {-M-_%*Almodo%*$.} {#NOP} {0};
#NOP #act {-M-_%*Packy%*$.} {#NOP} {0};
#NOP #act {-M-_%*Roor%*$.} {#NOP} {0};
#NOP #act {-M-_%*Ivy%*$.} {#NOP} {0};
#NOP #act {-M-_%*Martyr%*$.} {#NOP} {0};

/* load the selected bot's path into ready state */
#path load botpath;

/* pause the bot */
#alias {.pause} {
	#var halt 1;
};

/* kill the bot */
#alias {.stop} {
	.pause;
    #unvar botpath;
	#unalias {..};
	#unalias {.onPrompt};
	#unact {^>$};
	#class {bot} {kill};
	#class {xbot} {kill};
    #show Bot killed.;
};
#alias {.s} {.stop};

#NOP - If a monster is found that should be killed, the path will loop again.
#alias {.check_monster} {
    .prompt;#var foundmob 1;#var repeat_path 1;
    };

/* starts / continues the bot after it's loaded */
#alias {..} {
    #class {xbot} {open};
	#var halt 0;

    #act {{^-R-_}} {.onRoom} {0};

	#alias {.onRoom} {
		#var foundplayer 0;
		#var foundmob 0;
        .prompt;
	};

    #alias {.prompt} {
        #act {^>$} {.onPrompt};
    };

	#alias {.onPrompt} {
        #nop #show mob? $foundmob  player? $foundplayer;

        #if {$foundplayer} {
            .stop_prompt;
            #path walk;
        } {
            #if {$dontAttack} {
                #path walk;
            } {
                #if {$foundmob} {
                    #var ignoremob 0;
                    #foreach {$mobWhitelist} {name} {
                        #if {"%0" == "%*$name%*"} {
                            #var ignoremob 1;
                        };
                    };
                    #if {!$ignoremob} {
                        .stop_prompt;
                        k $mobname;
                    } {
                        .bot;
                    }
                    
                } {
                    .stop_prompt;
                    #path walk;
                }
            }
        };

        #if {$founditems} {
            #if {$pickupAll} {#nop get all;}
        };

    };

	#alias {.stop_prompt} {#unact {^>$}};
    
	glance;

	#class {xbot} {close};
};
#alias {.bot} {#if {$halt == 0} {..}};

#act {^%1 screeches in agony} {.bot} {0};
#act {^%1 gurgles in his own blood} {.bot} {0};
#act {^%1 gurgles in her own blood} {.bot} {0};
#act {^%1 gurgles in its own blood} {.bot} {0};
#act {^%1 gasps for breath, then slumps} {.bot} {0};



*/

/* Todo: make friends auto-defend list */
#act {{(.*) (\!)?attacking Toren\!}} {defend toren};

/* Signify there is a player in the room, if not in friends list */
#act {{^-P-_(.*)}} {
    #var foundfriend 0;
	#foreach {$playerWhitelist} {name} {
		#if {"%0" == "%*$name%*"} {
            #var foundfriend 1;
        };
	};
    #if {!$foundfriend} {#var foundplayer 1;};
} {1};



/* Bot end actions... */
#act {#END OF PATH} {
    #if {!$loop || !$repeat_path} {
        #show \nBot finished.;
        .stop;
    } {
        #show \nLooping bot...;
        #path load botpath;
        #var repeat_path 0;
        .bot;
    };
};


/* If for some reason we attack and mob isn't present, keep the bot going... */
#act {{^There is no (\w+) here}} {
    .bot;
};

/* helpers */
#function {botSetMobActions} {
	#var mobname %0;
    #var foundmob 1;
    #return;
};


#class {bot} {close}