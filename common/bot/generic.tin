#class {bot} {open}

#var botting 1;
#var loop 1;
#var playercheck 1;
#var bot_ticker 1;

#var foundmob 0;
#var foundplayer 0;
#var foundobject 0;
#var repeat_path 0;
#var prevMobName 0;
#var mobname 0;

#var dontAttack 0;


/* Read in the whitelist of players or followers to ignore */
#read common/bot/whitelist.tin;

/* load the selected bot's path into ready state */
#path load botpath;

/* 
    If gobj is necro, run the check death check 
    This is primarily a delay to let the corpse split / preserve both if you have mask
*/

#if {$guild == 'necromancer'} {
    #var check_death 1;
}


#alias playercheck {
    #if $playercheck {
        #var playercheck 0;
        #delay .2 {#showme <BAF><ffe>------ PLAYERCHECK IS NOW OFF --------<088>};
    } {
        #var playercheck 1;
        #delay .2 {#showme <BAF><ffe>------ PLAYERCHECK IS NOW ON --------<088>};
    }
};

/* pause the bot */
#alias {.pause} {
	#var halt 1;
};

/* kill the bot */
#alias {.stop} {
	.pause;
    #unvar botpath;
	#unalias {..};
	#unalias {.onPrompt};
	#unact {^>$};
	#class {bot} {kill};
	#class {xbot} {kill};
    #show Bot killed.;
};
#alias {.s} {.stop};


#NOP - If a monster is found that should be killed, the path will loop again.
#alias {.check_monster} {
    #var foundmob 1;
    #var repeat_path 1;
};

/* starts / continues the bot after it's loaded */
#alias {..} {
    #class {xbot} {open};
	#var halt 0;

    #act {{^-R-_(.*)-R-_}} { 
        #var room_short %%1;
        .onRoom;
    } {1};

	#alias {.onRoom} {
		#var foundplayer 0;
		#var foundmob 0;
        .prompt;
	};

    #alias {.prompt} {
        #act {^>$} {.onPrompt} {1};
    };

	#alias {.onPrompt} {
        #NOP #showme show mob? $foundmob, $mobname, player? $foundplayer, object? $foundobject;

        #if {$foundplayer || $dontAttack} {
            .stop_prompt;
            #path walk;
            #var bot_walking 1;
        } {
            #if {$foundmob} {
                .stop_prompt;   
                .pre_fight_check;
                #unact {{^-R-_(.*)-R-_}};
                #if {$mob_kill_actions} {
                    .mob_kill_action;
                } {
                    kill $mobname;
                };
            } {
                #if {$item_collector} {
                    get all;
                };
                #if {$foundobject} {#send $object_action;#var foundobject 0} {
                    #if {$check_room} {
                        .stop_prompt;
                        .check_room;
                    } {
                        .stop_prompt;
                        #path walk;
                        #var bot_walking 1;
                    };
                };
            };
        };
    };

    #alias {.stop_prompt} {#unact {^>$};#var bot_walking 0;};
    
	glance;

	#class {xbot} {close};
};
#alias {.bot} {#if {$halt == 0} {..}};

#action {%*dealt the killing blow%*} {#var foundmob 0;} {1};

#NOP #action {You give Byron a high-five!} {
#NOP     #echo {----- BOT REGISTERED KILL -----};
#NOP     #var foundmob 0;
#NOP     #if $check_death {
#NOP         #if {'$guild' == 'necromancer'} {
#NOP             #delay 3 {.check_death};
#NOP             #delay 4 {.bot};
#NOP         } {
#NOP             .check_death;
#NOP             .bot;
#NOP         }
#NOP     } {
#NOP         corpse_trig;
#NOP         .bot;
#NOP     };
#NOP } {1};



#action {{screeches in agony and falls to the ground, dead.|gasps for breath, then slumps into death.|gurgles in his own blood as he dies.|gurgles in her own blood as she dies.|gurgles in its own blood as it dies.}%*} {
    #echo {----- BOT REGISTERED KILL -----};
    #var foundmob 0;
    #if $check_death {
        #if {'$guild' == 'necromancer'} {
            #delay 3 {.check_death};
            #delay 4 {.bot};
        } {
            .check_death;
            .bot;
        }
    } {
        corpse_trig;
        .bot;
    };
} {1};


/* Signify there is a player in the room, if not in friends list */
#act {{^-P-_(.*)}} {
    #if $playercheck {
        #var foundplayer 1;
    }
} {2};

/* Todo: make friends auto-defend list */
#act {{(.*) (\!)?attacking Toren\!}} {defend toren};


/* Bot end actions... */
#act {#END OF PATH} {
    #if {!$loop || !$repeat_path} {
        #show \nBot finished.;
        #format bot_end_time {%T};
        #math bot_run_time {$bot_end_time - $bot_start_time};
        #show \nBot R;
        .stop;
    } {
        #show \nLooping bot...;
        #path load botpath;
        #var repeat_path 0;
        .bot;
    };
};


/* helpers */
#function {botSetMobActions} {
	#var mobname %1;
    #var foundtimes %2;
    #var foundmob 1;
    #return {};
};

/* Bot Stats */
#format bot_start_time {%T};


#class {bot} {close}